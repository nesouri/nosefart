dnl# -*- sh -*-
dnl# the "configure" script is made from this by running GNU "autoconf"
dnl#
dnl# Configuration script for nosefart xmms plugin
dnl#
dnl# by Benjamin Gerard <ben@sashipa.com>

AC_INIT(nosefart_xmms.c)
AM_INIT_AUTOMAKE(nosefart-xmms, 1.0.0)
AM_CONFIG_HEADER(xmms_config.h)

AC_ARG_ENABLE(debug,       [  --enable-debug          Compile sc68 with debug facilities [default=no]], DEBUG="yes", DEBUG="no")

if test $DEBUG = "yes"; then
    AC_DEFINE(DEBUG,1,Define if DEBUG mode is activated)
fi
DEL="rm -f"

AC_PROG_CC
AC_PROG_RANLIB
AM_PROG_LIBTOOL
AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_CHECK_FUNCS(vsprintf)
AC_CHECK_FUNCS(vsnprintf)

if test "x$cross_compiling" != "xyes"; then
    AC_C_BIGENDIAN
fi

AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)
AC_CHECK_SIZEOF(long long, 8)

if test $ac_cv_sizeof_char -eq 1; then
    AC_DEFINE(u8,unsigned char,unsigned 8 bit integer type)
    AC_DEFINE(s8,signed char, signed 8 bit integer type)
else
    AC_MSG_ERROR([Could not find 8 bit type])
fi

if test $ac_cv_sizeof_short -eq 2; then
    AC_DEFINE(u16,unsigned short,unsigned 16 bit integer type)
    AC_DEFINE(s16,signed short,signed 8 bit integer type)
else
    AC_MSG_ERROR([Could not find 16 bit type])
fi

if test $ac_cv_sizeof_int -eq 4; then
    AC_DEFINE(u32,unsigned int,unsigned 32 bit integer type)
    AC_DEFINE(s32,signed int,signed 32 bit integer type)
elif test $ac_cv_sizeof_long -eq 4; then
    AC_DEFINE(u32,unsigned long,unsigned 32 bit integer type)
    AC_DEFINE(s32,signed long,signed 32 bit integer type)
else
    AC_MSG_ERROR([Could not find 32 bit type])
fi

if test $ac_cv_sizeof_int -eq 8; then
    AC_DEFINE(u64,unsigned int,unsigned 64 bit integer type)
    AC_DEFINE(s64,signed int,signed 64 bit integer type)
elif test $ac_cv_sizeof_long -eq 8; then
    AC_DEFINE(u64,unsigned long,unsigned 64 bit integer type)
    AC_DEFINE(s64,signed long,signed 64 bit integer type)
elif test $ac_cv_sizeof_long_long -eq 8; then
    AC_DEFINE(u64,unsigned long long,unsigned 64 bit integer type)
    AC_DEFINE(s64,signed long long,signed 64 bit integer type)
fi


# XMMS
# -------------------------------------------------------------------
AC_PATH_PROG(XMMS_CONFIG, xmms-config, no)
if test "x$XMMS_CONFIG" != "xno"; then
    XMMS_CFLAGS=`"$XMMS_CONFIG" --cflags`
    XMMS_INPUT_PLUGIN_DIR=`"$XMMS_CONFIG" --input-plugin-dir`
    SUBDIRS68="$SUBDIRS68 xmms68"
else
    AC_MSG_ERROR(
[Could not find xmms-config
Get xmms-devel package or source code xmms at <http://www.xmms.org>])
fi

AC_SUBST(LIBTOOL_DEPS)
AC_SUBST(LIBCOMMAND)
AC_SUBST(MAKEDEPEND)
AC_SUBST(DEL)
AC_SUBST(XMMS_INPUT_PLUGIN_DIR)
AC_SUBST(XMMS_CFLAGS)
AC_SUBST(VERSION)

AC_OUTPUT([Makefile
])

AC_MSG_RESULT([**************************************])
AC_MSG_RESULT([* Compilation options summary:])
AC_MSG_RESULT([*   debug-mode        : $DEBUG])
AC_MSG_RESULT([*   xmms input plugin : $XMMS_INPUT_PLUGIN_DIR])
AC_MSG_RESULT([**************************************])

dnl#
dnl# End of "$Id: configure.in,v 1.5 2002/02/18 13:51:36 ben Exp $".
dnl#
